;
;	Tablacus DOS cmd
;
_CMD:
	DI
	CALL	FRMEVL
	PUSH	HL
	LD	H,080H			;ページ3をバックアップ
	LD	A,(BACKUP_RAM_3)
	CALL	MAPPER_PUT_PH
	LD	HL,0C000H
	LD	DE,08000H
	LD	BC,BDOS0-0C000H
	LDIR
	LD	H,080H
	LD	A,(BASIC_RAM_2)
	CALL	MAPPER_PUT_PH
	POP	HL
	PUSH	HL
	LD	(BASSP),SP

	CALL	FRESTR
	LD	B,0
	LD	C,(HL)
	INC	HL
	LD	A,(HL)
	INC	HL
	LD	H,(HL)
	LD	L,A
	LD	DE,BUFSTR
	LD	A,C
	OR	A
	JP	Z,WBOOT1
	LDIR
	XOR	A
	LD	(DE),A

	LD	H,040H
	LD	A,(DOS_RAM_1)
	CALL	MAPPER_PUT_PH
	LD	A,(RAMAD1)
	CALL	_ENASLT		;ページ1をRAMに切り替える

	LD	HL,ENASLT_00H
	LD	DE,04000H
	LD	BC,ENASLT_00H_E-ENASLT_00H
	LDIR

	LD	H,000H
	LD	A,(DOS_RAM_0)
	CALL	MAPPER_PUT_PH
	LD	A,(RAMAD0)
	CALL	04000H	;ページ0をRAMに切り替える

	LD	H,080H
	LD	A,(DOS_RAM_2)
	CALL	MAPPER_PUT_PH	;ページ2をDOSのRAMに切り替える

	LD	HL,FDRV
	LD	B,37
	XOR	A
ZERO_FCB:
	LD	(HL),A
	INC	HL
	DJNZ	ZERO_FCB

	LD	DE,BUFSTR
	CALL	FILE
	PUSH	DE

	LD	A,(DE)
	PUSH	AF
	EX	DE,HL
	LD	DE,BUFSTR
	XOR	A
	SBC	HL,DE
	LD	C,L
	LD	B,H
	LD	HL,BUFSTR
	LD	DE,COMSTR
	LDIR
	LD	HL,COM_EXT
	LD	BC,5
	LDIR
	LD	HL,N_PROGRAM
	LD	DE,COMSTR
	LD	C,06CH		;_SENV 環境変数の設定
	CALL	BDOS
	LD	DE,COMSTR
	LD	C,043H		;(BDOS)ファイルハンドルのオープン
	CALL	BDOS
	POP	HL
	OR	A
	POP	DE
	JR	NZ,WBOOT1
	LD	A,H
	LD	(DE),A
	PUSH	DE
	PUSH	BC
	LD	DE,00100H
	LD	HL,BDOS0-00100H
	LD	C,048H		;(BDOS)ファイルハンドルからの読み出し
	CALL	BDOS
	POP	BC
	LD	C,045H		;(BDOS)ファイルハンドルのクローズ
	CALL	BDOS
	POP	DE
	LD	HL,FCB1
ZERO1:
	LD	(HL),H
	INC	L
	JR	NZ,ZERO1

	LD	B,0
	LD	HL,DTA1+1
PARAM1:
	LD	A,(DE)
	LD	(HL),A
	OR	A
	JR	Z,PARAM2
	INC	DE
	INC	HL
	INC	B
	JR	PARAM1
PARAM2:
	LD	A,B
	LD	(DTA1),A

	LD	DE,DTA1+1
	CALL	FILE
	PUSH	DE
	LD	HL,FDRV
	LD	DE,FCB1
	LD	BC,00010H
	LDIR
	POP	DE
	LD	HL,FCB2
	CALL	FILE
	LD	HL,FDRV
	LD	DE,FCB2
	LD	BC,00010H
	LDIR
	LD	HL,N_PARAMETERS
	LD	DE,DTA1+1
	LD	C,06CH		;_SENV 環境変数の設定
	CALL	BDOS
	XOR	A
	LD	L,A
	LD	H,A
	LD	(TERMCODE),A
	LD	(DEFAB),HL
	LD	SP,BDOS0
	CALL	00100H
WBOOT1:
	DI
	LD	SP,(BASSP)

	LD	H,080H			;ページ3を復元
	LD	A,(BACKUP_RAM_3)
	CALL	MAPPER_PUT_PH
	LD	HL,08000H
	LD	DE,0C000H
	LD	BC,BDOS0-0C000H
	LDIR

	LD	H,080H			;ページ2をBASICのRAMに
	LD	A,(BASIC_RAM_2)
	CALL	MAPPER_PUT_PH

	LD	H,000H		;ページ0
	LD	A,(EXPTBL)	;メインROMスロット
	CALL	_ENASLT		;ページ0をBIOSに切り替える(RAMのENASLTはページ0切り替え可能)
	LD	H,000H			;ページ0をBASICのRAMに
	LD	A,(BASIC_RAM_0)
	CALL	MAPPER_PUT_PH

	LD	H,040H			;ページ1をBASICのRAMに
	LD	A,(BASIC_RAM_1)
	CALL	MAPPER_PUT_PH
	LD	A,(EXPTBL)	;メインROMスロット
	CALL	_ENASLT		;ページ1をBIOSに切り替える

	XOR	A
	LD	(CSRSW),A

	POP	HL
	EI
	RET

FILE:				;DOS2
	CALL	SPCUT
	PUSH	DE
	LD	C,05BH		;(BDOS) パス名の解析
	CALL	BDOS
	POP	DE
	INC	DE
	LD	A,(DE)
	CP	':'
	JR	NZ,FILED2_1
	LD	A,C
	LD	(FDRV),A
FILED2_1:
	EX	DE,HL
	LD	HL,FNAME
	LD	C,05CH		;(BDOS) ファイル名の解析
	JP	BDOS

SS1:
	INC	DE
SPCUT:				;スペースをカット
	LD	A,(DE)
	CP	020H
	JR	Z,SS1
	RET

CAP:
	CP	'a'
	RET	C
	CP	'z'+1
	RET	NC
	SUB	020H
	RET
CAP2:
	CALL	CAP
CAP3:
	CALL	CAP4
	CP	021H
	RET	NC
	LD	A,020H
	RET
CAP4:
	CP	5
	RET	NZ
	LD	A,0E5H
	RET

CALSLT_BIOS:
	LD	IY,(EXPTBL-1) ;メインROMスロット
	JP	_CALSLT

;
;ページ0専門のENASLT
;in
;A←スロット
;破壊
;ABCHL
;
; ページ0,3以外に転送して実行するのでリロケータブルに作る
ENASLT_00H:
	DI
	LD	H,A
	AND	3
	LD	C,A
	IN	A,(0A8H)
	LD	B,0FCH	;ページ0
	AND	B
	OR	C
	BIT	7,H
	JP	Z,ENASLT_NOEXT
				;拡張スロットの処理
	PUSH	DE
	LD	D,A	;基本スロットに出力する値
			;対象の拡張スロットを切り替えるために基本スロットを切り替える
	LD	A,H
	AND	3
	LD	C,A	;C=スロット#

	;スロットテーブル
	LD	L,LOW SLTTBL
	ADD	A,L
	LD	L,A
			;対象の拡張スロットを切り替えるために基本スロットを切り替える
	LD	A,C	;C=スロット#
	RRCA
	RRCA		;ページ3
	LD	C,A
	LD	A,D	;基本スロットに出力する値
	AND	03FH	;ページ3マスク
	OR	C
	LD	E,A	;基本スロットでページ3にスロットを割り当てる
	LD	A,H
	AND	00CH
	LD	H,HIGH SLTTBL
	RRCA
	RRCA
	LD	C,A
	LD	A,E
	OUT	(0A8H),A
	LD	A,(0FFFFH)	;拡張スロットの切り替え
	CPL
	AND	B
	OR	C
	LD	(0FFFFH),A
	LD	(HL),A
				;基本スロットの切り替え
	LD	A,D
	OUT	(0A8H),A
	POP	DE
	RET
ENASLT_00H_E:

N_PROGRAM:
	DB	"PROGRAM",0
N_PARAMETERS:
	DB	"PARAMETERS",0
COM_EXT:
	DB	".COM",0
TERMCODE:
	DB	0
BASSP:
	DW	0
